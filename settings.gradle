include 'builder'

if (!isBuilderTask()) {
	include 'android/android-sdk', 'android/android-all', 'android/services-v62', 'android/services-v7', 'android/services-v71', 'android/services-v72', 'android/services-v73', 'ios', 'windows'

	project(':android/android-all').name = 'liferay-android-sdk'
	project(':android/android-sdk').name = 'liferay-android-sdk-core'
	project(':android/services-v62').name = 'liferay-android-services-v62'
	project(':android/services-v7').name = 'liferay-android-services-v7'
	project(':android/services-v71').name = 'liferay-android-services-v71'
	project(':android/services-v72').name = 'liferay-android-services-v72'
	project(':android/services-v73').name = 'liferay-android-services-v73'
}

rootProject.name = 'liferay-mobile-sdk-project'

def modules = files { file('modules').listFiles() }

modules.each { File module ->
	if (module.isDirectory() && !module.isHidden()) {
		include "modules/${module.name}"
		project(":modules/${module.name}").name = "modules-${module.name}"
	}
}

def isBuilderTask() {
	def taskNames = gradle.startParameter.taskNames

	return taskNames.size() > 0 && (taskNames.get(0).equals("createModule") || taskNames.get(0).equals("generate") || taskNames.get(0).equals("jar"))
}