ext {
    portalClasspath = files(
        new File(portalDir + '/lib/development').listFiles(),
        new File(portalDir + '/lib/global').listFiles(),
        new File(portalDir + '/lib/portal').listFiles(),
        new File(portalDir + '/portal-impl/portal-impl.jar'),
        new File(portalDir + '/portal-service/portal-service.jar'),
        new File(portalDir + '/portal-web/portal-web.jar'),
        new File(portalDir + '/util-java/util-java.jar')
    )
}

subprojects {
    repositories {
        mavenCentral()
    }

    task format(type: JavaExec) {
        main = 'com.liferay.portal.tools.sourceformatter.SourceFormatter'
        classpath = portalClasspath
        workingDir = projectDir
    }
}

configure(subprojects.findAll {it.name != 'sdk-builder'}) {
	configurations {
		builder
	}

	dependencies {
		builder project(':sdk-builder')
	}

	task buildService(type: JavaExec) {
		main = 'com.liferay.mobile.sdk.MobileSDKBuilder'
		classpath = configurations.builder

		args 'platform=' + project.name
		args 'url=' + url
		args 'context=' + context
		args 'filter=' + filter
		args 'packageName=' + packageName
		args 'destination=' + destination
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}