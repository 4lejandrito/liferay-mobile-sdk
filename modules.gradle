def tasksGroup = 'Liferay'

configure(subprojects.findAll { it.name.startsWith('modules/') }) {
	archivesBaseName = "liferay-android-${context}"

	sourceSets {
		main {
			java {
				srcDir 'android/src/gen/java'
			}
		}
		test {
			java {
				srcDir 'android/src/test/java'
			}
		}
	}

	dependencies {
		compile project(':android')
		testCompile group: 'junit', name: 'junit', version: '4.+'
	}

	task zip(type: Zip) {
		group = tasksGroup
		description = 'Zips generated iOS SDK for this module.'
		baseName = "liferay-ios-${context}"
		destinationDir = project.buildDir
		version = project.version
		from 'ios/Source'
		doLast {
			println "Zipped to ${archivePath}"
		}
	}
}

task createModule << {
	def console = System.console()

	if (console) {
		def all = project.hasProperty('all')
		def context = readProperty('Context', project.ext['context'])
		def String platforms = 'android,ios'

		if (all) {
			platforms = readProperty('Platforms', project.ext['platforms'])
			def url = readProperty('Server URL', project.ext['url'])
			def filter = readProperty('Filter', project.ext['filter'], false)
			def portalVersion = readProperty('Portal Version', project.ext['portalVersion'])
			def version = readProperty('Module Version', project.version)
		}

		def moduleDir = "${project.rootDir}/modules/$context"
		file(moduleDir).mkdirs()

		def gradleProperties = file("$moduleDir/gradle.properties")
		gradleProperties.createNewFile()

		writeProperty(gradleProperties, 'context', context)

		if (all) {
			writeProperty(gradleProperties, 'platforms', platforms)
			writeProperty(gradleProperties, 'url', url)
			writeProperty(gradleProperties, 'filter', filter)
			writeProperty(gradleProperties, 'portalVersion', portalVersion)
			writeProperty(gradleProperties, 'version', version, project.version)
		}

		if (platforms.contains('android')) {
			def packageName = readProperty('Package Name', project.ext['packageName'])
			def description = readProperty('POM Description', project.description)

			writeProperty(gradleProperties, 'packageName', packageName)
			writeProperty(gradleProperties, 'description', description, project.description)
		}
	}
	else {
		logger.error "Error while getting console."
	}
}

createModule {
	group = tasksGroup
	description = "Creates SDK Builder modules."
}

def readProperty(phrase, property, required=true) {
	def console = System.console()
	def defaultValue = (property.isAllWhitespace()) ? '' : " [${property}]"

	def line = console.readLine("> ${phrase}${defaultValue}: ")

	if (!line.isAllWhitespace()) {
		property = line
	}

	if (required && property.isAllWhitespace()) {
		println "This property can't be empty, type again."
		property = readProperty(phrase, property, true)
	}

	return property
}

def writeProperty(file, key, value, defaultValue=project.ext[key]) {
	def n = System.getProperty("line.separator")

	if (defaultValue != value) {
		file.append("${key}=${value}${n}")
	}
}