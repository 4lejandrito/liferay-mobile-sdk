plugins {
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.6'
}

apply from: '../formatter.gradle'
format.workingDir = 'src/main/java'

apply from: '../sdk-builder.gradle'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifactId 'liferay-mobile-sdk'
			artifact sourceJar
		}
	}
}

bintray {
	user = property('user')
	key = property('key')

	publications = ['mavenJava']

	pkg {
		repo = 'liferay-mobile'
		name = 'liferay-mobile-sdk'
		userOrg = 'liferay'
		licenses = ['LGPL-2.1']
		vcsUrl = 'https://github.com/liferay/liferay-mobile-sdk.git'

		version {
			name = project.version
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'src/gen/java'
		}
	}
}

dependencies {
	compile group: 'com.google.android', name: 'android', version: '2.3.3'
	compile group: 'io.reactivex', name: 'rxjava', version: '1.1.3'
	compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
	compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'
	testCompile group: 'junit', name: 'junit', version: '4.12-beta-3'
}

task formatTest(type: JavaExec) {
	main = format.main
	classpath = format.classpath
	workingDir = 'src/test/java'
}

task sourceJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}